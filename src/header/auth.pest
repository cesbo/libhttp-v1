// --- RFC7230 ---

EOL = _{ "\n" }
ESC = _{ "\\" }
QUOTE = _{ "\"" }
WHITESPACE = _{ " " | "\t" }

TCHAR = _{
	ASCII_ALPHANUMERIC |
	"!" | "#" | "$" | "%" |
    "&" | "'" | "*" | "+" |
    "-" | "." | "^" | "_" |
    "`" | "|" | "~"
}

token = @{ TCHAR+ }

// --- String ---

escape = @{ QUOTE | ESC | " " }
fragment = @{ (!(ESC | EOL | QUOTE) ~ ANY)+ }

string = ${
    QUOTE ~
    (
        ESC ~ escape |
        fragment
    )* ~
    QUOTE
}

// --- Challenge ---

scheme = @{ token }
key = @{ token }
value = @{ string | token }
pair = { key ~ "=" ~ value }

challenge = { SOI ~ scheme ~ (pair ~ ","?)+ ~ EOI }
